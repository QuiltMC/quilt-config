plugins {
	id 'java'
	id 'maven-publish'
	id 'org.quiltmc.gradle.licenser' version '1.+'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group 'org.quiltmc'
version '1.2.0-beta.3'

java {
	withSourcesJar()
}

repositories {
	mavenCentral()
	maven {
		name = 'Quilt'
		url = 'https://maven.quiltmc.org/repository/release'
	}
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
	implementation 'com.electronwill.night-config:core:3.6.6'
	implementation 'com.electronwill.night-config:toml:3.6.6'
	implementation 'org.quiltmc.parsers:json:0.2.1'

	implementation 'org.jetbrains:annotations:23.0.0'
}

test {
	useJUnitPlatform()
}

jar {
	from ('.') {
		include 'LICENSE'
	}
}

license {
	rule rootProject.file("codeformat/HEADER")

	include "**/*.java"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// TODO: We should probably put this on central
	repositories {
		def ENV = System.getenv()

		if (ENV.MAVEN_URL) {
			project.logger.info("Publishing to " + ENV.MAVEN_URL)

			repositories.maven {
				url = ENV.MAVEN_URL

				credentials {
					username = ENV.MAVEN_USERNAME
					password = ENV.MAVEN_PASSWORD
				}
			}
		} else {
			project.logger.info("No Maven destination configured.")
		}
	}
}
